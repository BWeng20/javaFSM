/**
 * Build Script for variant with thrift-trace features turned on.
 * The source needs to be re-compiled with different flags for each variant.
 * The compile-time flags are defined in interface "StaticSettings" which is generated from a template.
 * The template is configured with "StaticOptionsConfig" below.
 */

plugins {
    id 'java'
}

apply from: rootProject.file('fsm-common.gradle')

dependencies {

    implementation "org.graalvm.sdk:graal-sdk:${graalvmVersion}"
    implementation "org.graalvm.js:js:${graalvmVersion}"

    implementation "org.apache.thrift:libthrift:${thriftVersion}"

    // Needed by thrift
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.slf4j:slf4j-simple:${slf4jVersion}"

    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"

    compileOnly 'org.jetbrains:annotations:26.0.2'
    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "net.sf.saxon:Saxon-HE:${saxonVersion}"
}


ext.'StaticOptionsConfig' =
        [
                'Debug' :
                        [
                                'On' : false,
                                'Serializer': false,
                                'Reader': false
                        ],
                'Trace' :
                        [
                                'On' : true,
                                'Method': true,
                                'Event': true,
                                'State': true,
                                'Script': true,
                                'Thrift': true
                        ],
                'Datamodels':
                        [
                                'ecma' : true
                        ]
        ]

tasks.named('compileJava') {
    dependsOn(tasks.named('generateThrift'))
}

sourceSets {
    main {
        java {
            srcDir '../src/main/java'
            srcDir thriftOutputDir
            srcDir 'build/generated/sources/configuration'
        }
        resources {
            srcDir '../src/main/resources'
        }
    }
    examples {
        java {
            srcDir '../src/examples/java'
        }
        resources {
            srcDir '../src/examples/resources'
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        runtimeClasspath += configurations.runtimeClasspath

    }
    test {
        java {
            srcDir '../src/test/java'
        }
        resources {
            srcDir '../src/test/resources'
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        runtimeClasspath += configurations.runtimeClasspath
    }
}
