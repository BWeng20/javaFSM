plugins {
    id 'java'
}

group = 'io.github.bweng20'
version = '1.0-SNAPSHOT'

apply from: rootProject.file('fsm-common.gradle')

dependencies {
    implementation "org.graalvm.sdk:graal-sdk:${graalvmVersion}"
    implementation "org.graalvm.js:js:${graalvmVersion}"

    implementation "org.apache.thrift:libthrift:${thriftVersion}"

    // Needed by thrift
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.slf4j:slf4j-simple:${slf4jVersion}"

    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"

    compileOnly 'org.jetbrains:annotations:26.0.2'
    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "net.sf.saxon:Saxon-HE:${saxonVersion}"
}

/** Main project has all options enabled */
ext.'StaticOptionsConfig' =
        [
                'Debug' :
                        [
                                'On' : true,
                                'Serializer': true,
                                'Reader': true
                        ],
                'Trace' :
                        [
                                'On' : true,
                                'Method': true,
                                'Event': true,
                                'State': true,
                                'Script': true,
                                'Thrift': true
                        ],
                'Datamodels':
                        [
                                'ecma' : true
                        ]
        ]

tasks.register('w3cTest', JavaExec) {
    group = 'verification'
    description = 'Starts the w3c test'

    mainClass = 'com.bw.fsm.W3CTest'

    classpath = sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
    println 'class path '+classpath.asPath

    // jvmArgs = ['-Xmx512m']
    args = [layout.projectDirectory.asFile.absolutePath + '/../w3ctest', "-report", layout.projectDirectory.asFile.absolutePath + "/../W3C_TEST_REPORT.MD", "-parallel", "-logOnlyFailure"]
}

tasks.register('w3cTestWithOptionals', JavaExec) {
    group = 'verification'
    description = 'Starts the w3c test, including optional tests'

    mainClass = 'com.bw.fsm.W3CTest'

    classpath = sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath

    // jvmArgs = ['-Xmx512m']
    args = [layout.projectDirectory.asFile.absolutePath + '/w3ctest', "-report", layout.projectDirectory.asFile.absolutePath + "/W3C_TEST_REPORT.MD", "-parallel", "-logOnlyFailure", "-optionals"]
}

tasks.named('compileJava') {
    dependsOn(tasks.named('generateThrift'))
}

sourceSets {
    main {
        java {
            srcDir '../src/main/java'
            srcDir thriftOutputDir
            srcDir 'build/generated/sources/configuration'
        }
        resources {
            srcDir '../src/main/resources'
        }
    }
    examples {
        java {
            srcDir '../src/examples/java'
        }
        resources {
            srcDir '../src/examples/resources'
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        runtimeClasspath += configurations.runtimeClasspath

    }
    test {
        java {
            srcDir '../src/test/java'
        }
        resources {
            srcDir '../src/test/resources'
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        runtimeClasspath += configurations.runtimeClasspath
    }
}
